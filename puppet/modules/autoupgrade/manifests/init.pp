class autoupgrade {

  include '::autoupgrade::params'

  [
    $apply,
    $hour_range,
    $hour,
    $minute,
    $monthday,
    $month,
    $weekday,
    $reboot,
    $commands_before_reboot,
    $puppet_run,
    $flag_no_puppet_run,
    $puppet_bin,
    $upgrade_wrapper,
    $upgrade_subcmd,
    $apt_clean,
    $supported_distributions,
    #
    $autoupgrade_script,
    $puppet_run_at_reboot_script,
    $flag_puppet_run_at_reboot,
    $logfile,
  ] = Class['::autoupgrade::params']

  ::homemade::is_supported_distrib($supported_distributions, $title)

  with($hour_range) |$range| {
    $min = $range[0]
    $max = $range[1]
    unless $min < $max {
      @("END"/L$).fail
        Class ${title}: the `hour_range` parameter of the `params` class \
        is not correct because the first element must be strictly \
        lower than the second.
        |-END
    }
  }

  # If it's an integer, the hour must be in the "hour" range.
  with($hour, $hour_range) |$hour, $range| {
    if $hour =~ Integer[0, 23] {
      $min = $range[0]
      $max = $range[1]
      unless ($min <= $hour) and ($hour < $max) {
        @("END"/L$).fail
          Class ${title}: the `hour` parameter of the `params` class \
          defines an hour (${hour}) which does not belong to the range \
          given by the parameter `hour_range` ie [$min, $max[.
          |-END
      }
    }
  }

  $final_hour = $hour.lest || {
    $min = $hour_range[0]
    $max = $hour_range[1]
    $min + fqdn_rand($max-$min, 'upgradereboot-hour')
  }

  case $apply {
    true: {
      $ensure_cron                      = 'present'
      $ensure_file                      = 'file'
      $ensure_file_puppet_run_at_reboot = if $puppet_run and $reboot { 'file' } else { 'absent' }
      $ensure_cron_puppet_run_at_reboot = if $puppet_run and $reboot { 'present' } else { 'absent' }
    }
    default: {
      $ensure_cron                      = 'absent'
      $ensure_file                      = 'absent'
      $ensure_file_puppet_run_at_reboot = 'absent'
      $ensure_cron_puppet_run_at_reboot = 'absent'
    }
  }

  # Required to "gzip" log file generated by the cron.
  ensure_packages(['gzip'], {ensure => present})

  $autoupgrade_name = $autoupgrade_script.basename

  file { $autoupgrade_script:
    ensure => $ensure_file,
    owner  => 'root',
    group  => 'root',
    mode   => '0750',
    content => epp( "autoupgrade/${autoupgrade_name}.epp",
                    {
                      'reboot'                    => $reboot,
                      'commands_before_reboot'    => $commands_before_reboot,
                      'puppet_run'                => $puppet_run,
                      'flag_no_puppet_run'        => $flag_no_puppet_run,
                      'puppet_bin'                => $puppet_bin,
                      'logfile'                   => $logfile,
                      'flag_puppet_run_at_reboot' => $flag_puppet_run_at_reboot,
                      'upgrade_subcmd'            => $upgrade_subcmd,
                      'apt_clean'                 => $apt_clean,
                    }
                  ),
  }

  $cron_cmd = case $upgrade_wrapper {
    Undef:   { $autoupgrade_script                        }
    default: { "${upgrade_wrapper} ${autoupgrade_script}" }
  }

  cron { 'cron-auto-upgrade':
    ensure   => $ensure_cron,
    user     => 'root',
    command  => $cron_cmd,
    hour     => $final_hour,
    minute   => $minute,
    monthday => $monthday,
    month    => $month,
    weekday  => $weekday,
    require  => File[$autoupgrade_script],
  }

  $puppet_run_at_reboot_name = $puppet_run_at_reboot_script.basename

  file { $puppet_run_at_reboot_script:
    ensure => $ensure_file_puppet_run_at_reboot,
    owner  => 'root',
    group  => 'root',
    mode   => '0750',
    content => epp( "autoupgrade/${puppet_run_at_reboot_name}.epp",
                    {
                      'puppet_bin'                => $puppet_bin,
                      'logfile'                   => $logfile,
                      'flag_puppet_run_at_reboot' => $flag_puppet_run_at_reboot,
                      'flag_no_puppet_run'        => $flag_no_puppet_run,
                    }
                  ),
  }

  cron { 'cron-puppet-run-at-reboot':
    ensure   => $ensure_cron_puppet_run_at_reboot,
    user     => 'root',
    command  => $puppet_run_at_reboot_script,
    special  => 'reboot',
  }

}


