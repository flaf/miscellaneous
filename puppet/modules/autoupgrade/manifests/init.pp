class autoupgrade {

  include '::autoupgrade::params'

  [
    $apply,
    $hour,
    $minute,
    $monthday,
    $month,
    $weekday,
    $reboot,
    $commands_before_reboot,
    $puppet_run,
    $puppet_bin,
    $upgrade_wrapper,
    $supported_distributions,
    #
    $autoupgrade_script,
    $puppet_run_at_reboot_script,
    $flag_puppet_run_at_reboot,
    $logfile,
  ] = Class['::autoupgrade::params']

  ::homemade::is_supported_distrib($supported_distributions, $title)

  case $apply {
    true: {
      $ensure_cron                      = 'present'
      $ensure_file                      = 'file'
      $ensure_file_puppet_run_at_reboot = if $puppet_run and $reboot { 'file' } else { 'absent' }
      $ensure_cron_puppet_run_at_reboot = if $puppet_run and $reboot { 'present' } else { 'absent' }
    }
    default: {
      $ensure_cron                      = 'absent'
      $ensure_file                      = 'absent'
      $ensure_file_puppet_run_at_reboot = 'absent'
      $ensure_cron_puppet_run_at_reboot = 'absent'
    }
  }

  # Required to "gzip" log file generated by the cron.
  ensure_packages(['gzip'], {ensure => present})

  $autoupgrade_name = $autoupgrade_script.basename

  file { $autoupgrade_script:
    ensure => $ensure_file,
    owner  => 'root',
    group  => 'root',
    mode   => '0750',
    content => epp( "autoupgrade/${autoupgrade_name}.epp",
                    {
                      'reboot'                    => $reboot,
                      'commands_before_reboot'    => $commands_before_reboot,
                      'puppet_run'                => $puppet_run,
                      'puppet_bin'                => $puppet_bin,
                      'logfile'                   => $logfile,
                      'flag_puppet_run_at_reboot' => $flag_puppet_run_at_reboot,
                    }
                  ),
  }

  $cron_cmd = case $upgrade_wrapper {
    Undef:   { $autoupgrade_script                        }
    default: { "${upgrade_wrapper} ${autoupgrade_script}" }
  }

  cron { 'cron-auto-upgrade':
    ensure   => $ensure_cron,
    user     => 'root',
    command  => $cron_cmd,
    hour     => $hour,
    minute   => $minute,
    monthday => $monthday,
    month    => $month,
    weekday  => $weekday,
    require  => File[$autoupgrade_script],
  }

  $puppet_run_at_reboot_name = $puppet_run_at_reboot_script.basename

  file { $puppet_run_at_reboot_script:
    ensure => $ensure_file_puppet_run_at_reboot,
    owner  => 'root',
    group  => 'root',
    mode   => '0750',
    content => epp( "autoupgrade/${puppet_run_at_reboot_name}.epp",
                    {
                      'puppet_bin'                => $puppet_bin,
                      'logfile'                   => $logfile,
                      'flag_puppet_run_at_reboot' => $flag_puppet_run_at_reboot,
                    }
                  ),
  }

  cron { 'cron-puppet-run-at-reboot':
    ensure   => $ensure_cron_puppet_run_at_reboot,
    user     => 'root',
    command  => $puppet_run_at_reboot_script,
    special  => 'reboot',
  }

}


